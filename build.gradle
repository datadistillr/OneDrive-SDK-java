plugins {
	id 'java-library'
	id 'idea'
	id 'com.google.osdetector' version '1.7.3'
	id 'io.freefair.lombok' version "8.0.1"
	id 'maven-publish'
}

group 'org.onedrive'
version '0.10.3'

compileJava {
	sourceCompatibility = 1.7
	targetCompatibility = 1.7
}

compileTestJava {
	sourceCompatibility = 1.8
	targetCompatibility = 1.8
}

repositories {
	jcenter()
}

ext {
	nettyVer = '4.1.86.Final'
	jacksonVer = '2.14.2'
}

dependencies {
	implementation "com.fasterxml.jackson.core:jackson-annotations:$jacksonVer"
	implementation "com.fasterxml.jackson.core:jackson-core:$jacksonVer"

	api "io.netty:netty-buffer:$nettyVer"
	api "io.netty:netty-codec:$nettyVer"
	api "io.netty:netty-codec-http:$nettyVer"
	api "io.netty:netty-common:$nettyVer"
	api "io.netty:netty-handler:$nettyVer"
	api "io.netty:netty-resolver:$nettyVer"
	api "io.netty:netty-transport:$nettyVer"


	if (osdetector.os == 'linux') {
		implementation "io.netty:netty-transport-native-epoll:4.+:${osdetector.classifier}"
	}
	else if (osdetector.os == 'osx' || osdetector.os.endsWith('bsd')) {
		implementation "io.netty:netty-transport-native-kqueue:4.+:${osdetector.classifier}"
	}

	api 'org.jetbrains:annotations:+'

	testImplementation "com.fasterxml.jackson.core:jackson-databind:$jacksonVer"
	testImplementation 'org.junit.jupiter:junit-jupiter-api:5.+'
	testImplementation 'org.junit.jupiter:junit-jupiter-engine:5.+'
}

test {
	useJUnitPlatform()
}

javadoc {
	if (JavaVersion.current() >= JavaVersion.VERSION_1_9) {
		options.addBooleanOption('html5').setValue(true)
	}
}

task sourcesJar(type: Jar) {
	archiveClassifier = 'sources'
	from sourceSets.main.allJava
}

task javadocJar(type: Jar) {
	archiveClassifier = 'javadoc'
	from javadoc.destinationDir
}

artifacts {
	archives jar
	archives sourcesJar
	archives javadocJar
}

publishing {
	publications {
		maven(MavenPublication) {
			groupId = group
			artifactId = 'OneDrive-SDK-java'
			version = version
			from components.java

			pom {
				description = 'A fast, easy to use, intuitive Java API for OneDrive.'
			}
		}
	}

	repositories {
		maven { url "https://jitpack.io" }
	}
}